class is SpriteEntity is PointerListener is KeyListener imports cape imports cave imports motion :

var scene as GameScene
var ponySprite as TextureSprite
var height as double
var width as double
var spriteX as double
var spriteY as double
var pointerX as double
var pointerY as double
var isClicked = false
var isPressedKeyUp = false
var isPressedKeyDown = false
var isPressedKeyRight = false
var isPressedKeyLeft = false

func createSprite override as Sprite
{
	scene = getScene() as GameScene
	height = scene.getReferenceHeight()
	width = scene.getReferenceWidth()
	ponySprite = scene.addTextureSpriteForSize(scene.createTextureForImageResource("pinkypiesprite"), 0.1 * width, 0.1 * height)
	ponySprite.move(0.1 * width - (ponySprite.getWidth() * 0.5), 0.5 * height - (ponySprite.getHeight() * 0.5))
	return ponySprite
}

func onPointerEvent(event as PointerEvent) as bool
{
	if(event.action == PointerEvent.DOWN) {
		isClicked = true
	}
	if(event.action == PointerEvent.UP) {
		isClicked = false
	}
	pointerX = event.getX()
	pointerY = event.getY()
	return false
}

func tick(gameTime as TimeValue, delta as double) override
{
	base.tick(gameTime, delta)
	spriteX = sprite.getX()
	spriteY = sprite.getY()
	if (isClicked) {
		spriteX -= (sprite.getX() - (pointerX + 0.5 * sprite.getWidth())) * 0.1
		spriteY -= (sprite.getY() - (pointerY + 0.5 * sprite.getHeight())) * 0.1
		sprite.move(spriteX, spriteY)
	}
	else if (isPressedKeyUp) {
		sprite.move(spriteX, sprite.getY() - height * 0.01)
	}
	else if (isPressedKeyDown) {
		sprite.move(spriteX, sprite.getY() + height * 0.01)
	}
	else if (isPressedKeyLeft) {
		sprite.move(spriteX - width * 0.01, spriteY)
	}
	else if (isPressedKeyRight) {
		sprite.move(spriteX + width * 0.01, spriteY)
	}
}

func onKeyEvent(event as KeyEvent)
{
	if(event.isKey(KeyEvent.KEY_UP)) {
		isPressedKeyUp = true
	}
	else if(event.isKey(KeyEvent.KEY_DOWN)) {
		isPressedKeyDown = true
	}
	else if(event.isKey(KeyEvent.KEY_LEFT)) {
		isPressedKeyLeft = true
	}
	else if(event.isKey(KeyEvent.KEY_RIGHT)) {
		isPressedKeyRight = true
	}
	else {
		PRINT("Unhandled key event: `".. event.getStringValue() .."'")
	}
	if (event.getAction() == 2) {
		isPressedKeyUp = false
		isPressedKeyDown = false
		isPressedKeyLeft = false
		isPressedKeyRight = false
	}
}

func cleanup override
{
	scene.removeAllSprites()
}
